fun main() : nothing 

    fun swap(ref a,b : int) : nothing
        var tmp : int;
    {
    tmp <- a;
    a <- b;
    b <- tmp;
    }

    fun partition(ref array : int[]; low,high : int) : int
        var i,j,pivot : int;
    {
    
    $ select the rightmost element as pivot
    pivot <- array[high];
    
    $ pointer for greater element
    i <- low - 1;

    $ traverse each element of the array
    $ compare them with the pivot
    j <- low;
    while (j < high) do {
        if (array[j] <= pivot) then {
            
        $ if element smaller than pivot is found
        $ swap it with the greater element pointed by i
        i <- i+1;
        
        $ swap element at i with element at j
        swap(array[i], array[j]);
        }
        j <- j+1;
    }

    $ swap the pivot element with the greater element at i
    swap(array[i + 1], array[high]);
    
    $ return the partition point
    return (i + 1);
    }

    fun quickSort(ref array : int[]; low,high : int) : nothing
        var pi : int;
    {
    if (low < high) then {
        
        $ find the pivot element such that
        $ elements smaller than pivot are on left of pivot
        $ elements greater than pivot are on right of pivot
        pi <- partition(array, low, high);
        
        $ recursive call on the left of pivot
        quickSort(array, low, pi - 1);
        
        $ recursive call on the right of pivot
        quickSort(array, pi + 1, high);
    }
    }

    $ function to print array elements
    fun writeArray (ref msg : char[]; n : int; ref x : int[]) : nothing
        var i : int;
    {
        writeString(msg);
        i <- 0;
        while i < n do {
            if i > 0 then writeString(", ");
            writeInteger(x[i]);
            i <- i+1;
        }
        writeString("\n");
    }

    var x : int[16];
    var seed,i,N : int;

{ $ main
    N <- 16;
    seed <- 65;
    i <- 0;
    while i < 16 do {
        seed <- (seed * 137 + 221 + i) mod 101;
        x[i] <- seed;
        i <- i+1;
    }
    writeArray("Initial array: ", N, x);
    $ perform quicksort on data
    quickSort(x, 0, N-1);
    writeArray("Sorted  array: ", N, x);
 } $ main